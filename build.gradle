plugins {
    id 'com.github.johnrengelman.shadow' version '7.0.0'
    id 'io.franzbecker.gradle-lombok' version '3.2.0'
    id 'org.cadixdev.licenser' version '0.6.0'
    id 'java-library'
}

group 'dev.vankka'
version '4.2.4-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()

    if (version.endsWith('-SNAPSHOT')) { // Permit snapshots only when the version is a snapshot
        maven {
            url 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
        }
    }
}

javadoc {
    options.memberLevel = JavadocMemberLevel.PUBLIC
    options.author()
    options.encoding = 'UTF-8'
    options.addBooleanOption('html5', true)
    options.addBooleanOption('Xdoclint:none', true)
    failOnError = false
}

lombok {
    version = '1.18.10'
    sha256 = ''
}

test {
    reports {
        junitXml.enabled = false
        html.enabled = true
    }
    ignoreFailures = true
    subprojects.each { dependsOn(it.path + ':test') }
}

license {
    header = rootProject.file('LICENSE_HEADER')
    properties {
        String inception = '2018'
        String currentYear = Calendar.getInstance().get(Calendar.YEAR)

        year = inception == currentYear ? currentYear : inception + '-' + currentYear
    }
    include '**/*.java' // only java files
}

dependencies {
    testImplementation group: 'junit', name: 'junit', version: '4.13.2'
    api group: 'net.kyori', name: 'adventure-api', version: '4.1.0'
    api group: 'dev.vankka', name: 'SimpleAST', version: '2.2.7'
}

jar {
    dependsOn licenseFormat
    finalizedBy shadowJar
}

shadowJar {
    configurations = [project.configurations.runtimeClasspath]
    classifier 'shaded'
}

java {
    withJavadocJar()
    withSourcesJar()
}

if (System.getenv('SONATYPE_KEY') == null && !project.hasProperty("signing.keyId")) {
    // Don't even bother with signing/publishing if neither of these are set
    return
}

task publishProject {
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    dependsOn tasks.build

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java

                pom {
                    name = project.name
                    packaging = 'jar'
                    description = 'A library for transcoding between Minecraft and Discord. '
                    url = 'https://github.com/Vankka/MCDiscordReserializer'

                    scm {
                        connection = 'scm:git:https://github.com/Vankka/MCDiscordReserializer.git'
                        developerConnection = 'scm:git:https://github.com/Vankka/MCDiscordReserializer.git'
                        url = 'https://github.com/Vankka/MCDiscordReserializer'
                    }

                    licenses {
                        license {
                            name = 'GNU General Public License, Version 3.0'
                            url = 'https://www.gnu.org/licenses/gpl-3.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id = 'Vankka'
                        }
                    }
                }
            }
        }

        signing {
            sign publishing.publications.maven

            def key = System.getenv('SONATYPE_KEY')
            if (key != null) {
                useInMemoryPgpKeys(key, System.getenv('SONATYPE_KEY_PASS'))
            }
        }

        repositories {
            maven {
                credentials {
                    username findProperty('ossrhUsername')
                    password findProperty('ossrhPassword')
                }
                url version.endsWith('-SNAPSHOT') ? 'https://s01.oss.sonatype.org/content/repositories/snapshots/' : 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            }
        }
    }

    finalizedBy publish
}
